buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java'
    id 'com.gradleup.shadow' version '8.3.6' apply false
}

group = 'be.yvanmazy'
version = '1.0'

dependencies {
    subprojects.each { final project ->
        implementation project
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'com.gradleup.shadow'

    group = rootProject.group
    version = rootProject.version

    repositories {
        mavenCentral()
    }

    dependencies {
        compileOnly 'org.jetbrains:annotations:26.0.2'

        if (project.name != 'common') {
            implementation project(":common")
        }

        // Redis
        implementation 'redis.clients:jedis:5.2.0'
    }

    java {
        toolchain {
            languageVersion.set(JavaLanguageVersion.of(17))
        }
    }

    shadowJar {
        if (project == rootProject) {
            archiveBaseName.set(rootProject.name)
        } else {
            archiveBaseName.set(rootProject.name + '-' + project.name)
        }
        archiveClassifier.set('')

        if (project.name == 'spigot' || project.name == project.rootProject.name) {
            relocate 'org.yaml.snakeyaml', 'be.yvanmazy.proxyonlinelinker.libs.snakeyaml'
        }
    }

}